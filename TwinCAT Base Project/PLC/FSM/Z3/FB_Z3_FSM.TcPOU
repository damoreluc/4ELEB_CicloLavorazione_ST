<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_Z3_FSM" Id="{c31aae44-2349-0766-0980-89d2bc93340f}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Z3_FSM
VAR_INPUT
	bEnable		:	BOOL;
	bAck		:	BOOL;
	bStop		:	BOOL;
	bI6			:	BOOL;
	stParams	:	ST_Z3_Params;	
END_VAR
VAR_IN_OUT
	stStatus	:	ST_FSM_Status;
END_VAR
VAR_OUTPUT
	// Digital Outputs
	bQ6			:	BOOL;
	bQ7			:	BOOL;	
	bErr		:	BOOL;
END_VAR

VAR
	// FSM internal variables
	bInitialized	:	BOOL;
	eNewstep		:	E_Z3_Step;
	bEntryact		:	BOOL;
	fbTonStep		:	TON;
	
	fbRtStop		:	R_TRIG;
	fbRtI6			:	R_TRIG;
	bStopRequest	:	BOOL;

	// object on conveyor
	bConveyorLoaded	:	BOOL;	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF NOT bInitialized THEN
	eNewstep :=  E_Z3_Step.NoStep;
	stStatus.stZ3.eStep :=  E_Z3_Step.NeOrError;
	bEntryact := FALSE;
	bInitialized := TRUE;	
	bErr := FALSE;
	stStatus.stZ3.eErrorStep :=  E_Z3_Step.NoStep;	
	stStatus.stZ3.nErrorId := 0;	
ELSE
	(* fronti *)
	fbRtStop(CLK:= bStop);	
	fbRtI6(CLK := bI6);
	
	(* richieste *)
	IF fbRtStop.Q THEN
		bStopRequest := TRUE;
	END_IF; 

	(* disabilitata o avaria *)
	IF NOT bEnable THEN
		eNewstep := E_Z3_Step.NeOrError;
	END_IF;
	
	(* aggiornamento dello stato *)
	IF eNewstep <>  E_Z3_Step.NoStep THEN
		stStatus.stZ3.eStep := eNewstep;
		eNewstep :=  E_Z3_Step.NoStep;
		bEntryact := FALSE;
	END_IF;	
	
	(* switch - case della MSF *)
	CASE stStatus.stZ3.eStep OF
		
		// NeOrError ---------------------------------
		E_Z3_Step.NeOrError :
			// on entry action
			IF NOT bEntryact THEN
				bErr := TRUE;
				bEntryact := TRUE;
			END_IF
			
			// body
			bQ6 := FALSE;
			bQ7 := FALSE;					
			
			// next state transition
			IF bEnable THEN
				IF bAck THEN
					eNewStep := E_Z3_Step.Recovering;
				END_IF
			END_IF
		
			// on exit action
			IF eNewStep <> E_Z3_Step.NoStep THEN
				stStatus.stZ3.nErrorId := 0;
				stStatus.stZ3.eErrorStep := E_Z3_Step.NoStep;
			END_IF
	
		// Recovering ---------------------------------			
		E_Z3_Step.Recovering :
			// on entry action
			IF NOT bEntryact THEN
				bErr := FALSE;				
				bEntryact := TRUE;
			END_IF
			
			// body	
			bQ6 := FALSE;
			bQ7 := FALSE;

			// next state transition
			IF bEnable THEN
				IF (stStatus.stZ3.tStepTime > stParams.tRecovering) THEN
					eNewStep := E_Z3_Step.Stop;
				END_IF				
			END_IF
			
			// on exit action
			IF eNewStep <> E_Z3_Step.NoStep THEN
				;
			END_IF			
			
		// Stop ---------------------------------------
		E_Z3_Step.Stop :
			// on entry action
			IF NOT bEntryact THEN
				bErr:= FALSE;				
				bStopRequest := FALSE;
				bConveyorLoaded := FALSE;
				bEntryact := TRUE;
			END_IF
			
			// body	
			bQ6 := FALSE;
			bQ7 := FALSE;
			
			// next state transition
			IF bEnable THEN
				IF (stStatus.stZ3.tStepTime > stParams.tStopDelay) THEN
					eNewStep := E_Z3_Step.Ready;
				END_IF
			END_IF
			
			// on exit action
			IF eNewStep <> E_Z3_Step.NoStep THEN
				;
			END_IF
			
		// Ready ---------------------------------------
		E_Z3_Step.Ready :
			// on entry action
			IF NOT bEntryact THEN
				bErr:= FALSE;				
				bConveyorLoaded := FALSE;
				bEntryact := TRUE;
			END_IF
			
			// body	
			bQ6 := FALSE;
			bQ7 := FALSE;

			// next state transition
			IF bStopRequest THEN 
				eNewStep := E_Z3_Step.Stop;
			ELSIF stStatus.stZ2.eStep=E_Z2_Step.WaitZ3Ready THEN
				eNewStep := E_Z3_Step.MoveToMill;
			END_IF
			
			// on exit action
			IF eNewStep <> E_Z3_Step.NoStep THEN
				;
			END_IF
			
			// MoveToMill ---------------------------------------
		E_Z3_Step.MoveToMill :
			// on entry action
			IF NOT bEntryact THEN
				bErr:= FALSE;				
				bConveyorLoaded := TRUE;
				bEntryact := TRUE;
			END_IF
			
			// body	
			bQ6 := NOT(bI6);
			bQ7 := FALSE;
	
			// next state transition
			IF bStopRequest THEN 
				eNewStep := E_Z3_Step.Recovering;
			ELSIF bI6 THEN
				eNewStep := E_Z3_Step.Milling;
			END_IF
			
			// on exit action
			IF eNewStep <> E_Z3_Step.NoStep THEN
				;
			END_IF
						

		// Milling ---------------------------------------
		E_Z3_Step.Milling :
			// on entry action
			IF NOT bEntryact THEN
				bErr:= FALSE;
				bConveyorLoaded := TRUE;
				bEntryact := TRUE;
			END_IF
			
			// body	
			bQ6 := FALSE;
			bQ7 := TRUE;
			
			// next state transition
			IF stStatus.stZ3.tStepTime > stParams.tMilling THEN 
				eNewStep := E_Z3_Step.WaitZ4Ready;
			END_IF
			
			// on exit action
			IF eNewStep <> E_Z3_Step.NoStep THEN
				;
			END_IF
			
		// WaitZ4Ready ---------------------------------------
		E_Z3_Step.WaitZ4Ready :
			// on entry action
			IF NOT bEntryact THEN
				bErr:= FALSE;
				bConveyorLoaded := TRUE;
				bEntryact := TRUE;
			END_IF
			
			// body	
			bQ6 := FALSE;
			bQ7 := FALSE;
			
			// next state transition
			IF stStatus.stZ4.eStep=E_Z4_Step.Ready THEN 
				eNewStep := E_Z3_Step.MoveToZ4;
			END_IF
			
			// on exit action
			IF eNewStep <> E_Z3_Step.NoStep THEN
				;
			END_IF
			
		// MoveToZ4 ---------------------------------------
		E_Z3_Step.MoveToZ4 :
			// on entry action
			IF NOT bEntryact THEN
				bErr:= FALSE;
				bConveyorLoaded := TRUE;
				bEntryact := TRUE;
			END_IF
			
			// body	
			bQ6 := FALSE;
			bQ7 := TRUE;
			
			// next state transition
			IF stStatus.stZ3.tStepTime > stParams.tDelayToZ4 THEN 
				eNewStep := E_Z3_Step.Done;
			END_IF
			
			// on exit action
			IF eNewStep <> E_Z3_Step.NoStep THEN
				;
			END_IF
			
		// Done ---------------------------------------
		E_Z3_Step.Done :
			// on entry action
			IF NOT bEntryact THEN
				bErr:= FALSE;
				bConveyorLoaded := FALSE;
				bEntryact := TRUE;
			END_IF
			
			// body	
			bQ6 := FALSE;
			bQ7 := FALSE;
			
			// next state transition
			eNewStep := E_Z3_Step.Ready;
			
			
			// on exit action
			IF eNewStep <> E_Z3_Step.NoStep THEN
				;
			END_IF
			
	ELSE
			eNewstep :=  E_Z3_Step.NeOrError;
			stStatus.stZ3.eErrorStep :=  E_Z3_Step.Unknown;			
			stStatus.stZ3.nErrorId := 16#0003_9999;	
	END_CASE
	
	(* call FBs *)
	
	(* tempo di permanenza nello step *)
	fbTonStep(IN := (eNewstep =  E_Z3_Step.NoStep), PT := T#500H);
	stStatus.stZ3.tStepTime := fbTonStep.ET;	
	
END_IF	]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>